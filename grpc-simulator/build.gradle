apply plugin: 'com.google.protobuf'

ext {
    grpc_version        = '1.27.1'
    protobuf_version    = '3.11.0'

    genDir              = file("${projectDir}/src/gen")
}

dependencies {
    compile "com.exactpro.th2:th2-grpc-infra:${commonVersion}"

    //for support jdk 11
    compile "javax.annotation:javax.annotation-api:1.3.2"

    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobuf_version}"

    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

compileJava.dependsOn.add('generateProto')

task zipProtobuf (type: Zip) {
    classifier 'protobuf'

    from sourceSets.main.proto.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'th2-grpc-simulator'

            artifact source: zipProtobuf, extension: 'zip'
        }
    }
}

artifactoryPublish.skip = false

clean {
    delete genDir
}