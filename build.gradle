/*******************************************************************************
 *  Copyright 2020 Exactpro (Exactpro Systems Limited)
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 ******************************************************************************/

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
    }
}

plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id 'com.palantir.docker' version '0.25.0' apply false
    id 'com.jfrog.artifactory' version '4.15.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    ext {
        sharedDir           = file("${project.rootDir}/shared")

        commonVersion       = '1.1-SNAPSHOT'
    }

    group = 'com.exactpro.th2'
    version = "${version_major}.${version_minor}-SNAPSHOT"

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

    artifactoryPublish.skip = true

    artifactory {
        publish {
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = true
                // Properties to be attached to the published artifacts.
                publishPom = true
            }
        }
    }
}

apply plugin: 'com.palantir.docker'
apply plugin: 'application'
apply plugin: "org.jetbrains.kotlin.jvm"

ext {
    dockerImageVersion          = "${version_major}.${version_minor}.${version_maintenance}.${version_build}"
    version_slf4j               = '1.7.30'

    genDir              = file('src/gen')
}

dependencies {
    compile project('grpc-simulator')

    compile "org.slf4j:slf4j-api:${version_slf4j}"
    compile "org.slf4j:slf4j-log4j12:${version_slf4j}"

    compile "com.exactpro.th2:th2-common:${commonVersion}"
    compile "com.exactpro.th2:th2-grpc-connectivity:${commonVersion}"

    testImplementation 'junit:junit:4.12'
}

distTar {
    archiveName "${archivesBaseName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    name "${target_docker_repository}/${docke_image_name}:${dockerImageVersion}"
    tag 'local-version', "${docke_image_name}:${dockerImageVersion}"
    
    copySpec.from(tarTree("$buildDir/distributions/${archivesBaseName}.tar"))
}

application {
    mainClassName = "com.exactpro.th2.simulator.run.SimulatorServerMain"
}

jar {
    from sourceSets.test.output+sourceSets.test.allSource
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    main.java.srcDirs += "src/main/java"
}

artifactoryPublish.skip = false
